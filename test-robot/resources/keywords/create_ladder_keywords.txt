*** Keywords ***
Click Home Page Create Ladder Link
    Safe Click Element    //a[@id='create-ladder-link']    error=Failed to click home page ladder creation button

Fill In Create Ladder Form
    [Arguments]    ${create_ladder_form}
    Enter Ladder Name    ${create_ladder_form['name']}
    Enter Ladder Rungs    ${create_ladder_form['rungs']}
    Select Ladder Privacy    ${create_ladder_form['is_private']}
    Select Ladder Type    ${create_ladder_form['type']}

Enter Ladder Name
    [Arguments]    ${name}
    Safe Input Text    //input[@id='id_name']    ${name}    error=Failed to enter ladder name
    
Enter Ladder Rungs
    [Arguments]    ${rungs}
    Safe Input Text    //input[@id='id_rungs']    ${rungs}    error=Failed to enter ladder rungs

Select Ladder Privacy
    [Arguments]    ${is_private}
    Run Keyword If True    ${is_private}    Safe Select Checkbox    //input[@id='id_is_private']    error=Failed to choose ladder privacy

Select Ladder Type
    [Arguments]    ${type}
    Safe Select From List    //select[@id='id_type']    ${type}    error=Failed to select ladder type

Click Create Ladder Button
    Safe Click Element    //input[@id='create-ladder-submit']    error=Failed to click ladder creation button

Complete Create Ladder Form
    [Arguments]    ${create_ladder_form}
    Fill In Create Ladder Form    ${create_ladder_form}
    Click Create Ladder Button